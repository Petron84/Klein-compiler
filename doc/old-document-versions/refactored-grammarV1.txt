<PROGRAM> ::=     <DEFINITION-LIST>

    <DEFINITION-LIST> ::= ε
                        | <DEFINITION> <DEFINITION-LIST>

         <DEFINITION> ::= "function" <IDENTIFIER> "(" <PARAMETER-LIST> ")" ":" <TYPE> <BODY>

     <PARAMETER-LIST> ::= ε
                        | <FORMAL-PARAMETERS>

  <FORMAL-PARAMETERS> ::= <ID-WITH-TYPE> <FORMAL-PARAMETERS'>

 <FORMAL-PARAMETERS'> ::= ε
			| "," <FORMAL-PARAMETERS>

       <ID-WITH-TYPE> ::= <IDENTIFIER> ":" <TYPE>

               <TYPE> ::= "integer"
                        | "boolean"

               <BODY> ::= <PRINT-EXPRESSION> <BODY>
                        | <EXPRESSION>

   <PRINT-EXPRESSION> ::= "print" "(" <EXPRESSION> ")"

	 <EXPRESSION> ::= <SIMPLE-EXPRESSION> <EXPRESSION'>

	<EXPRESSION'> ::= ε
			| "=" <SIMPLE-EXPRESSION>
			| "<" <SIMPLE-EXPRESSION>

  <SIMPLE-EXPRESSION> ::= <TERM> <SIMPLE-EXPRESSION'>

 <SIMPLE-EXPRESSION'> ::= ε
			| "or" <TERM>
			| "+" <TERM>
			| "-" <TERM>


               <TERM> ::= <FACTOR> <TERM'>

	      <TERM'> ::= ε
			| "*"  <FACTOR>
			| "/" <FACTOR>
			| "and" <FACTOR>

             <FACTOR> ::= <LITERAL>
                        | "not" <FACTOR>
                        | "-" <FACTOR>
                        | <IDENTIFIER> <FACTOR'>
                        | "if" <EXPRESSION> "then" <EXPRESSION> "else" <EXPRESSION>
                        | "(" <EXPRESSION> ")"

	    <FACTOR'> ::= ε
			| "(" <ARGUMENT-LIST> ")"

      <ARGUMENT-LIST> ::= ε
                        | <FORMAL-ARGUMENTS>

   <FORMAL-ARGUMENTS> ::= <EXPRESSION> <FORMAL-ARGUMENTS'>

  <FORMAL-ARGUMENTS'> ::= ε
			| "," <FORMAL-ARGUMENTS>

            <LITERAL> ::= <INTEGER-LITERAL>
                        | <BOOLEAN-LITERAL>